// create a runnable jar with jar dependencies stored in lib subdirectory
tasks.whenTaskAdded { task ->
    ['startScripts', 'distTar'].each { String skipTaskName ->
        if (task.name.contains(skipTaskName)) {
            task.enabled = false
        }
    }
}
apply plugin: 'java'
apply plugin: 'application'

java {
    sourceCompatibility = 17
    targetCompatibility = 17
}
mainClassName = 'edu.mcw.rgd.dataload.omim.Manager'
String myAppName = 'omim-pipeline'

project.archivesBaseName = myAppName

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-dbcp2:2.13.0'
    implementation 'commons-net:commons-net:3.11.1'
    implementation ('org.apache.httpcomponents:httpclient:4.5.14') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
    implementation 'org.apache.logging.log4j:log4j-core:2.24.1'
    implementation 'com.oracle.database.jdbc:ojdbc11:23.8.0.25.04'
    implementation 'org.springframework:spring-jdbc:6.2.5'

    implementation fileTree(dir: 'lib', include: '*.jar')
}

jar {
    manifest {
        attributes(
            'Built-By': System.getProperty('user.name'),
            'Built-Date': new Date(),
            'Built-JDK': System.getProperty('java.version'),
            'Class-Path': configurations.runtimeClasspath.collect { it.getName() }.join(' '),
            'Main-Class': mainClassName
        )
    }
}

distributions {
    main {
        distributionBaseName = myAppName
    }
}

task createDistro(type: Copy) {
    def zipFile = file('build/distributions/'+myAppName+'.zip')
    def outputDir = file("build/install")

    from zipTree(zipFile)
    into outputDir
}
createDistro.dependsOn assembleDist
